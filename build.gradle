plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.3.0' apply false
}

subprojects {
    group = 'org.codefirst.wsdl2kotlin'
    version = '0.8.0-SNAPSHOT'

    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/suer/wsdl2kotlin")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
    }

    configurations {
        ktlint
    }

    dependencies {
        implementation platform('org.jetbrains.kotlin:kotlin-bom:2.3.0')
        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'

        ktlint "com.pinterest.ktlint:ktlint-cli:1.8.0"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        mainClass = "com.pinterest.ktlint.Main"
        args "src/**/*.kt"
    }
    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        mainClass = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}
